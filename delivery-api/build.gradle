buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.8.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'codenarc'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'uk.co.kenfos.api.Application'

repositories {
    jcenter()
}

configurations {
    compile.exclude(group: 'org.apache.tomcat')
    compile.exclude(group: 'org.apache.tomcat.embed')

    compile.exclude(module: 'spring-boot-cli')
    compile.exclude(module: 'spring-boot-starter-tomcat')
}

dependencies {
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-jersey')
    compile('org.springframework.boot:spring-boot-starter-undertow')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-web')

    compile('org.codehaus.groovy:groovy:2.4.5')
    compile('com.google.guava:guava:20.0')
    compile('joda-time:joda-time:2.9')
    compile('org.jadira.usertype:usertype.core:4.0.0.GA')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-hibernate:2.0.0-RC2')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-joda:2.6.3')
    compile('uk.co.modular-it:hamcrest-date:0.9.5')
    compile('org.unitils:unitils-core:3.4.3')

    compile('com.h2database:h2')
    compile('com.zaxxer:HikariCP:2.4.2')
    compile('mysql:mysql-connector-java:5.1.37')

    compile('org.springframework.integration:spring-integration-core:2.2.0.RELEASE')
    compile('org.springframework.integration:spring-integration-amqp:2.2.0.RELEASE')
    compile('org.codehaus.jackson:jackson-mapper-asl:1.9.13')

    runtime('org.liquibase:liquibase-core:3.4.1')

    testCompile('software.betamax:betamax-junit:2.0.1')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework:spring-test')
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.0-groovy-2.4')
    testCompile('com.jayway.restassured:rest-assured:2.9.0')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE')
    testCompile('cglib:cglib-nodep:3.2.4')
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir 'build/asciidoc'
    sourceDir 'src/main/asciidoc'
    dependsOn test
}

jar {
    baseName = 'kenfos-api'
    version =  '0.1.0'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/documentation'
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/test-ruleset.groovy')
}

task codenarc {
    dependsOn codenarcMain, codenarcTest
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
    bootRun.systemProperties = System.properties
}

task resolveAllDependencies() {
    doLast {
        configurations.all { it.resolve() }
    }
}