buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'codenarc'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'uk.co.kenfos.api.Application'

repositories {
    jcenter()
}

dependencies {
    compile('com.google.guava:guava:21.0')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.codehaus.groovy:groovy:2.4.7')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.3')
    compile('com.h2database:h2:1.4.193')
    compile('mysql:mysql-connector-java:6.0.5')

    runtime('org.liquibase:liquibase-core:3.4.1')

    testCompile('software.betamax:betamax-junit:2.0.1')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.0-groovy-2.4')
    testCompile('com.jayway.restassured:rest-assured:2.9.0')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE')
    testCompile('org.unitils:unitils-core:3.4.3')
    testCompile('cglib:cglib-nodep:3.2.4')
}

ext {
    snippetsDir = file('build/generated-snippets')
}

bootRun {
    systemProperties = System.properties
}

test {
    systemProperties = System.properties
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir 'build/asciidoc'
    sourceDir 'src/main/asciidoc'
    dependsOn test
}

jar {
    baseName = 'delivery-api'
    version =  '0.1.0'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/documentation'
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/test-ruleset.groovy')
}

task codenarc {
    dependsOn codenarcMain, codenarcTest
}

task resolveAllDependencies() {
    doLast {
        configurations.all { it.resolve() }
    }
}
